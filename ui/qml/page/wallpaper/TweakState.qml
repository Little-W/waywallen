pragma ComponentBehavior: Bound
import QtCore
import QtQml

QtObject {
    id: root

    readonly property int layoutFillCell: 0
    readonly property int layoutFixed: 1
    readonly property int minimumItemSize: 112
    readonly property int maximumItemSize: 260
    readonly property int itemSizeStep: 8
    property string settingsCategory: "WallpaperView"
    property int itemSize: 162
    property real itemAspectRatio: 1
    property int layoutMode: layoutFillCell
    readonly property real itemHeight: itemSize / Math.max(itemAspectRatio, 0.1)

    signal userChanged

    readonly property Settings settings: Settings {
        category: root.settingsCategory
        property alias itemSize: root.itemSize
        property alias itemAspectRatio: root.itemAspectRatio
        property alias layoutMode: root.layoutMode
    }

    Component.onCompleted: {
        applyLayout(itemSize, itemAspectRatio, layoutMode);
    }

    function normalizeItemSize(size) {
        const stepped = Math.round(Number(size) / itemSizeStep) * itemSizeStep;
        return Math.max(minimumItemSize, Math.min(maximumItemSize, stepped));
    }

    function normalizeItemAspectRatio(ratio) {
        const next = Number(ratio);
        return next > 0 ? next : 1;
    }

    function normalizeLayoutMode(mode) {
        return mode === layoutFixed ? layoutFixed : layoutFillCell;
    }

    function applyLayout(size, ratio, mode) {
        itemSize = normalizeItemSize(size);
        itemAspectRatio = normalizeItemAspectRatio(ratio);
        layoutMode = normalizeLayoutMode(mode);
    }

    function setItemSize(size) {
        itemSize = normalizeItemSize(size);
        userChanged();
    }

    function setItemAspectRatio(ratio) {
        itemAspectRatio = normalizeItemAspectRatio(ratio);
        userChanged();
    }

    function setLayoutMode(mode) {
        layoutMode = normalizeLayoutMode(mode);
        userChanged();
    }
}
